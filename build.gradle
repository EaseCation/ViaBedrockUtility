import org.oryxel.viabedrockutility.generator.EnumGeneratorTask;

plugins {
	id "org.jetbrains.gradle.plugin.idea-ext" version "1.1.10"
	id 'fabric-loom' version '1.13.6'
	id 'maven-publish'
	id 'dev.kikugie.stonecutter'
}

version = project.mod_version
group = project.maven_group

// Stonecutter 版本配置
def mcVersion = stonecutter.current.project
def versionConfig = [
	'1.21.5': [
		yarn: '1.21.5+build.1',
		fabricApi: '0.119.5+1.21.5',
		clothConfig: '18.0.145',
		modMenu: '14.0.1'
	],
	'1.21.6': [
		yarn: '1.21.6+build.1',
		fabricApi: '0.125.2+1.21.6',
		clothConfig: '19.0.147',
		modMenu: '15.0.1'
	],
	'1.21.7': [
		yarn: '1.21.7+build.1',
		fabricApi: '0.128.1+1.21.7',
		clothConfig: '19.0.147',
		modMenu: '15.0.1'
	],
	'1.21.8': [
		yarn: '1.21.8+build.1',
		fabricApi: '0.134.0+1.21.8',
		clothConfig: '19.0.147',
		modMenu: '15.0.1'
	],
	'1.21.9': [
		yarn: '1.21.9+build.1',
		fabricApi: '0.134.1+1.21.9',
		clothConfig: '20.0.149',
		modMenu: '16.0.0'
	],
	'1.21.10': [
		yarn: '1.21.10+build.3',
		fabricApi: '0.138.4+1.21.10',
		clothConfig: '20.0.149',
		modMenu: '16.0.0'
	],
	'1.21.11': [
		yarn: '1.21.11+build.4',
		fabricApi: '0.141.3+1.21.11',
		clothConfig: '21.11.153',
		modMenu: '17.0.0-beta.2',
		sodiumApi: '0.8.4+mc1.21.11'
	]
][mcVersion]

project.ext.minecraft_version = mcVersion
project.ext.yarn_mappings = versionConfig.yarn
project.ext.fabric_version = versionConfig.fabricApi
project.ext.minecraft_dep = "~1.21-"

base {
	archivesName = "${project.archives_base_name}-mc${mcVersion}"
}

configurations {
	implementation.extendsFrom lib
	api.extendsFrom lib
}

loom {
	accessWidenerPath = rootProject.file("src/main/resources/viabedrockutility.accesswidener")
}

repositories {
	mavenLocal()
	maven {
		name = "Jitpack"
		url = "https://jitpack.io"

		content {
			includeGroup "com.github.EaseCation"
		}
	}
	maven {
		name = "Shedaniel"
		url = "https://maven.shedaniel.me/"
	}
	maven {
		name = "Terraformers"
		url = "https://maven.terraformersmc.com/"
	}
	maven {
		name = "CaffeineMC"
		url = "https://maven.caffeinemc.net/releases"
	}
}

dependencies {
	minecraft "net.minecraft:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	lib('com.github.EaseCation:BedrockMotion:1.0.0') {
		exclude group: 'org.apache.commons', module: 'commons-lang3'
		exclude group: 'com.google.code.gson', module: 'gson'
	}

	modImplementation 'nakern.be_camera:bedrockcameralib:1.1.0'

	lib 'org.projectlombok:lombok:1.18.36'
	annotationProcessor 'org.projectlombok:lombok:1.18.36'

	// Optional dependencies for ModMenu config screen (not bundled in jar)
	modCompileOnly("me.shedaniel.cloth:cloth-config-fabric:${versionConfig.clothConfig}") {
		exclude(group: "net.fabricmc.fabric-api")
	}
	modCompileOnly "com.terraformersmc:modmenu:${versionConfig.modMenu}"

	// Optional Sodium Config API (only available for 1.21.11+)
	if (versionConfig.sodiumApi != null) {
		modCompileOnly "net.caffeinemc:sodium-fabric-api:${versionConfig.sodiumApi}"
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": inputs.properties.version,
			"minecraft_dep": project.minecraft_dep
	}

	// Copy binary files directly from libs/ to bypass Stonecutter's UTF-8 text processing
	from(rootProject.file("libs/vanilla_packs")) {
		into "assets/viabedrockutility/vanilla_packs"
		include "*.mcpack"
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

def enumSourceDir = layout.buildDirectory.dir("generated/enums/main/java")
def enumGeneratorTask = tasks.register("enumGeneratorTask", EnumGeneratorTask) {
	outputDirectory = enumSourceDir
}
tasks.compileJava.dependsOn(enumGeneratorTask)

sourceSets.main.java.srcDir(enumSourceDir)

idea {
	module {
		["run"].each {
			excludeDirs << file("$it")
		}
	}
}

if (project == rootProject) {
	idea.project.settings {
		taskTriggers {
			afterSync enumGeneratorTask
		}
	}
}

java {
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	inputs.property "archivesName", project.base.archivesName

	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}"}
	}

	dependsOn configurations.lib
	from {
		duplicatesStrategy = DuplicatesStrategy.EXCLUDE
		configurations.lib.collect {
			zipTree(it)
		}
	}
}
