import org.oryxel.viabedrockutility.generator.EnumGeneratorTask;

plugins {
	id "org.jetbrains.gradle.plugin.idea-ext" version "1.1.10"
	id 'fabric-loom' version '1.13.6'
	id 'maven-publish'
	id 'dev.kikugie.stonecutter'
}

version = project.mod_version
group = project.maven_group

// Stonecutter 版本配置
def mcVersion = stonecutter.current.project
def versionConfig = [
	'1.21.5': [
		yarn: '1.21.5+build.1',
		fabricApi: '0.119.5+1.21.5'
	],
	'1.21.6': [
		yarn: '1.21.6+build.1',
		fabricApi: '0.125.2+1.21.6'
	],
	'1.21.7': [
		yarn: '1.21.7+build.1',
		fabricApi: '0.128.1+1.21.7'
	],
	'1.21.8': [
		yarn: '1.21.8+build.1',
		fabricApi: '0.134.0+1.21.8'
	],
	'1.21.9': [
		yarn: '1.21.9+build.1',
		fabricApi: '0.134.1+1.21.9'
	],
	'1.21.10': [
		yarn: '1.21.10+build.3',
		fabricApi: '0.138.4+1.21.10'
	],
	'1.21.11': [
		yarn: '1.21.11+build.4',
		fabricApi: '0.141.3+1.21.11'
	]
][mcVersion]

project.ext.minecraft_version = mcVersion
project.ext.yarn_mappings = versionConfig.yarn
project.ext.fabric_version = versionConfig.fabricApi
project.ext.minecraft_dep = "~1.21-"

base {
	archivesName = "${project.archives_base_name}-mc${mcVersion}"
}

configurations {
	implementation.extendsFrom lib
	api.extendsFrom lib
}

loom {
	accessWidenerPath = rootProject.file("src/main/resources/viabedrockutility.accesswidener")
}

repositories {
	maven {
		name = "Jitpack"
		url = "https://jitpack.io"

		content {
			includeGroup "com.github.Oryxel"
		}
	}
}

dependencies {
	minecraft "net.minecraft:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	lib("com.github.Oryxel:CubeConverter:916cadb12a") {
		transitive = false
	}

	lib 'org.projectlombok:lombok:1.18.36'
	annotationProcessor 'org.projectlombok:lombok:1.18.36'

	lib("team.unnamed:mocha:3.0.1") {
		transitive = false
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": inputs.properties.version,
			"minecraft_dep": project.minecraft_dep
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

def enumSourceDir = layout.buildDirectory.dir("generated/enums/main/java")
def enumGeneratorTask = tasks.register("enumGeneratorTask", EnumGeneratorTask) {
	outputDirectory = enumSourceDir
}
tasks.compileJava.dependsOn(enumGeneratorTask)

sourceSets.main.java.srcDir(enumSourceDir)

idea {
	module {
		["run"].each {
			excludeDirs << file("$it")
		}
	}
}

if (project == rootProject) {
	idea.project.settings {
		taskTriggers {
			afterSync enumGeneratorTask
		}
	}
}

java {
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	inputs.property "archivesName", project.base.archivesName

	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}"}
	}

	dependsOn configurations.lib
	from {
		duplicatesStrategy = DuplicatesStrategy.EXCLUDE
		configurations.lib.collect {
			zipTree(it)
		}
	}
}
